plugins {
	id 'java'
	id 'maven'
}

group = 'de.uni-bremen.informatik.st'
version = '1.3.2'

repositories {
	mavenCentral()
}

configurations {
	extraLibs
}

dependencies {
	extraLibs project(':conqat')
	compile 'de.uni-bremen.informatik.st:libvcs4j-api:1.3.2'
	compile 'org.apache.commons:commons-lang3:3.4'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:2.18.3'
	testCompile 'org.assertj:assertj-core:3.10.0'
	configurations.compile.extendsFrom(configurations.extraLibs)
}

jar {
	from {
		configurations.extraLibs.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

build {
	doLast {
		pom {
			project {
				name 'libvcs4j-metrics'
				description 'The Metrics Module of LibVCS4j'
				url 'https://github.com/uni-bremen-agst/libvcs4j'
				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/mit-license.php'
					}
				}
				developers {
					developer {
						name 'Marcel Steinbeck'
						email 'marcel@informatik.uni-bremen.de'
						organization 'AG Softwaretechnik (University of Bremen)'
						organizationUrl 'https://www.informatik.uni-bremen.de/st'
					}
				}
				scm {
					connection 'scm:git:git://github.com/uni-bremen-agst/libvcs4j.git'
					developerConnection 'scm:git:ssh://github.com:uni-bremen-agst/libvcs4j.git'
					url 'https://github.com/uni-bremen-agst/libvcs4j'
				}
			}
		}.writeTo("${buildDir}/libs/pom.xml")
	}
}
